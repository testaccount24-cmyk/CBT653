 IDENTIFICATION DIVISION.                                         12/27/88
                                                                  D3ALT00P
 PROGRAM-ID.       D3ALT00P.                                         LV001

 AUTHOR.           JIM HARRISON.

 DATE-COMPILED.

 DATE-WRITTEN.     NOV 14, 1984.
******************************************************************
******                                                      ******
******  D3ALDRP - DRIVER TO CALL DYNAMIC ALLOCATION ROUTINES******
******            ON-LINE FOR TESTING PURPOSES.             ******
******                                                      ******
******************************************************************
**    CALLS:                                                    **
**       DDALLOC - DYNAMIC ALLOCATION (DDNAME AND/OR DATASET)   **
**       DDUNAL - DYNAMIC UNALLOCATION (FREE)                   **
******************************************************************
     EJECT
 ENVIRONMENT DIVISION.

 CONFIGURATION SECTION.

 SOURCE-COMPUTER.   IBM-370-168.

 OBJECT-COMPUTER.   IBM-370-168.

 INPUT-OUTPUT SECTION.

 FILE-CONTROL.
     EJECT
 DATA DIVISION.

 FILE SECTION.

 WORKING-STORAGE SECTION.
 77  PAN-VALET PICTURE X(47) VALUE
     '001D3ALT00P  12/27/88 COMPILED 89123 - 14:20:44'.

 01  ISPF-NAME-LISTS.
     05  LEN-44-LIST                     PIC X(9)
           VALUE '(DSNAME)'.
     05  LEN-8-LIST                      PIC X(30)
           VALUE '(DDNAME MEMNAME SYSODEST) '.
     05  LEN-6-LIST                      PIC X(20)
           VALUE '(VOLSER DEVTYPE)'.
     05  LEN-5-LIST                      PIC X(40)
           VALUE '(BLKSIZE RECODE RSCODE PRISPC) '.
     05  LEN-4-LIST                      PIC X(40)
           VALUE '(FORMAT LRECL)'.
     05  LEN-3-LIST                      PIC X(40)
           VALUE '(SECSPC DSORG DIRQTY RECFM)'.
     05  LEN-2-LIST                      PIC X(40)
           VALUE '(EROPT BUFNO SYSOCOPY SVCERROR)   '.
     05  LEN-1-LIST.
         10  FILLER                      PIC X(45)
               VALUE '(INIT NORM COND SPTYPE RLFLAG ROFLAG '.
         10  FILLER                      PIC X(40)
               VALUE 'CLFLAG SYSOCLAS SYSODISP OPTION)'.

 01  ISPF-WS.
     05  LEN-44-WS.
         10  DSNAME                      PIC X(44).
     05  LEN-8-WS.
         10  DDNAME                      PIC X(8).
         10  MEMNAME                     PIC X(8).
         10  SYSODEST                    PIC X(8).
     05  LEN-6-WS.
         10  VOLSER                      PIC X(6).
         10  DEVTYPE                     PIC X(6).
     05  LEN-5-WS.
         10  BLKSIZE                     PIC X(5).
         10  RECODE                      PIC X(5).
         10  RSCODE                      PIC X(5).
         10  PRISPC                      PIC X(5).
     05  LEN-4-WS.
         10  SFORMAT                     PIC X(4).
         10  LRECL                       PIC X(4).
     05  LEN-3-WS.
         10  SECSPC                      PIC X(3).
         10  DSORG                       PIC X(3).
         10  DIRQTY                      PIC X(3).
         10  RECFM                       PIC X(3).
     05  LEN-2-WS.
         10  EROPT                       PIC X(2).
         10  BUFNO                       PIC X(2).
         10  SYSOCOPY                    PIC X(2).
         10  SVCERROR                    PIC X(2).
     05  LEN-1-WS.
         10  INIT                        PIC X(1).
         10  NORM                        PIC X(1).
         10  COND                        PIC X(1).
         10  SPTYPE                      PIC X(1).
         10  RLFLAG                      PIC X(1).
         10  ROFLAG                      PIC X(1).
         10  CLFLAG                      PIC X(1).
         10  SYSOCLAS                    PIC X(1).
         10  SYSODISP                    PIC X(1).
         10  OPTION                      PIC X(1).

 01  MISC-FIELDS.
     05  EXIT-FLAG                       PIC X(1).
     05  SCREEN                          PIC X(08)
           VALUE 'SDALT00 '.

         COPY DDUNAL.
**********************************************************
*   COPYLIB MEMBER-NAME  - DDALLOC                       *
*   PANVALET MEMBER-NAME - D3CP021P                      *
**********************************************************
*   USED IN CALL TO MODULE DDALLOC (D3ALLOCP)            *
**********************************************************
* NOTE: THIS IS A NEW VERSION OF THE 'SDALLO' COPYAREA.  *
* IT IS NOT COMPATABLE WITH THE PREVIOUS VERSION.        *
**********************************************************
* RESP COMPONENT...:  OSI/OSIE/DDA/DRMB     12/2/87  JRH *
**********************************************************

 01  DDALLOC-ALLOCATION-FIELDS.
     05  A-DDNAME                        PIC X(8).
     05  A-DATASET-PARAMETERS.
         10  A-DSNAME                    PIC X(44).
             88  ALLOCATION-IS-TO-SYSOUT
                                     VALUE 'SYSOUT '.
             88  ALLOCATION-IS-TO-TERMINAL
                                     VALUE '*'.
             88  ALLOCATION-IS-TO-DUMMY
                                     VALUE 'DUMMY'.
         10  A-MEMNAME                   PIC X(8).
         10  A-DISPOSITION               PIC X(1).
             88  ALLOCATION-IS-NEW   VALUE 'N'.
             88  ALLOCATION-IS-OLD   VALUE 'O'.
             88  ALLOCATION-IS-SHR   VALUE 'S'.
             88  ALLOCATION-IS-MOD   VALUE 'M'.
         10  A-NORMAL-DISP               PIC X(1).
             88  NORMAL-DISP-IS-CATALOG
                                     VALUE 'C'.
             88  NORMAL-DISP-IS-KEEP VALUE 'K'.
             88  NORMAL-DISP-IS-DELETE
                                     VALUE 'D'.
         10  A-CONDITIONAL-DISP          PIC X(1).
             88  CONDITIONAL-DISP-IS-CATALOG
                                     VALUE 'C'.
             88  CONDITIONAL-DISP-IS-KEEP
                                     VALUE 'K'.
             88  CONDITIONAL-DISP-IS-DELETE
                                     VALUE 'D'.
         10  A-PRIMARY-SPACE             PIC 9(3).
         10  A-PRIMARY-SPACE-AN    REDEFINES
            A-PRIMARY-SPACE              PIC X(3).
         10  A-SECONDARY-SPACE           PIC 9(3).
         10  A-SECONDARY-SPACE-AN  REDEFINES
            A-SECONDARY-SPACE            PIC X(3).
         10  A-SPACE-TYPE                PIC X(1).
             88  ALLOCATE-SPACE-IN-TRACKS
                                     VALUE 'T'.
             88  ALLOCATE-SPACE-IN-CYLINDERS
                                     VALUE 'C'.
             88  ALLOCATE-SPACE-IN-BLOCKS
                                     VALUE 'B'.
         10  A-RLSE-INDICATOR            PIC X(1).
             88  RELEASE-UNUSED-SPACE
                                     VALUE 'Y'.
         10  A-ROUND-INDICATOR           PIC X(1).
             88  ROUND-UP-TO-CYLINDER
                                     VALUE 'Y'.
         10  A-CLOSE-INDICATOR           PIC X(1).
             88  UNALLOC-AT-CLOSE    VALUE 'Y'.
***
***  SPACE FORMAT: ALX, MXIG, CONTIG
***
         10  A-SPACE-FORMAT              PIC X(4).
         10  A-DEVICE                    PIC X(6).
         10  A-VOL-SER                   PIC X(6).

     05  A-DCB-PARAMETERS.
***
***  DSORG:  PS, PO, DA, PSU, POU, DAU, VS
***
         10  A-DSORG                     PIC X(3).
         10  A-LRECL                     PIC 9(4).
         10  A-LRECL-AN              REDEFINES A-LRECL
                                         PIC X(4).
         10  A-BLOCKSIZE                 PIC 9(5).
         10  A-BLOCKSIZE-AN          REDEFINES A-BLOCKSIZE
                                         PIC X(5).
         10  A-DIRECTORY                 PIC 9(3).
         10  A-DIRECTORY-AN          REDEFINES A-DIRECTORY
                                         PIC X(3).
***
***  RECFM:  F, FB, FBA, V, VB, VBA
***
         10  A-RECFM                     PIC X(3).
         10  A-ERROR-OPTION              PIC X(2).
             88  EROPT-ACCEPT        VALUE 'AC'.
             88  EROPT-SKIP          VALUE 'SK'.
             88  EROPT-ABEND         VALUE 'AB'.
         10  A-NUMBER-OF-BUFFERS         PIC S9(3)       COMP.

     05  A-SYSOUT-PARAMETERS.
         10  A-SYSOUT-CLASS              PIC X(1).
         10  A-SYSOUT-DEST               PIC X(8).
         10  A-SYSOUT-DISP               PIC X(1).
             88  HOLD-SYSOUT         VALUE 'H'.
         10  A-SYSOUT-COPIES             PIC S9(3)       COMP.
     05  A-ERROR-CODES.
         10  A-ERROR-REASON              PIC S9(4)       COMP.
     05  A-PRIMARY-SPACE2                PIC X(5).
     05  FILLER                          PIC X(45).
         COPY SDCNCT.
         COPY SDISP00.
     EJECT
 PROCEDURE DIVISION.
     PERFORM 1000-INIT-RTN
        THRU 1000-EXIT.
     PERFORM 100-MAIN-LOOP
        THRU 100-EXIT
             UNTIL EXIT-FLAG EQUAL TO 'Y'.
     MOVE SPACES TO ISPF-PARAMETERS.
     MOVE 'VDELETE' TO I-FUNCTION.
     MOVE '*' TO I-PARM1.
     PERFORM 200-CALL-ISPF
        THRU 200-EXIT.
     GOBACK.

 100-MAIN-LOOP.

     MOVE SPACES TO ISPF-PARAMETERS.
     MOVE PANEL-DISPLAY TO I-FUNCTION.
     MOVE SCREEN TO I-PARM1.
     PERFORM 200-CALL-ISPF
        THRU 200-EXIT.

     IF RETURN-CODE GREATER THAN 0
         MOVE 'Y' TO EXIT-FLAG
         GO TO 100-EXIT.

     MOVE ZEROS TO RETURN-CODE.

     IF OPTION EQUAL TO 'A'
         PERFORM 300-ALLOCATE
            THRU 300-EXIT
     ELSE
         IF OPTION EQUAL 'U'
             PERFORM 600-UNALLOCATE
                THRU 600-EXIT.

 100-EXIT.
     EXIT.
     EJECT
**************************************************************

 300-ALLOCATE.
**************************************************************
     MOVE DSNAME TO A-DSNAME.
     MOVE DDNAME TO A-DDNAME.
     MOVE MEMNAME TO A-MEMNAME.
     MOVE INIT TO A-DISPOSITION.
     MOVE NORM TO A-NORMAL-DISP.
     MOVE COND TO A-CONDITIONAL-DISP.

     IF PRISPC NUMERIC
        MOVE PRISPC TO A-PRIMARY-SPACE2
     ELSE
        MOVE SPACES TO A-PRIMARY-SPACE2.

     IF SECSPC NUMERIC
        MOVE SECSPC TO A-SECONDARY-SPACE-AN
     ELSE
        MOVE SPACES TO A-SECONDARY-SPACE-AN.

     MOVE SPTYPE TO A-SPACE-TYPE.
     MOVE RLFLAG TO A-RLSE-INDICATOR.
     MOVE ROFLAG TO A-ROUND-INDICATOR.
     MOVE CLFLAG TO A-CLOSE-INDICATOR.
     MOVE SFORMAT TO A-SPACE-FORMAT.
     MOVE DEVTYPE TO A-DEVICE.
     MOVE VOLSER TO A-VOL-SER.
     MOVE DSORG TO A-DSORG.

     IF LRECL NUMERIC
         MOVE LRECL TO A-LRECL-AN
     ELSE
         MOVE SPACES TO A-LRECL-AN.

     IF BLKSIZE NUMERIC
         MOVE BLKSIZE TO A-BLOCKSIZE-AN
     ELSE
         MOVE SPACES TO A-BLOCKSIZE-AN.

     IF DIRQTY NUMERIC
        MOVE DIRQTY TO A-DIRECTORY-AN
     ELSE
        MOVE SPACES TO A-DIRECTORY-AN.

     MOVE RECFM TO A-RECFM.
     MOVE EROPT TO A-ERROR-OPTION.

     IF BUFNO NUMERIC
         MOVE BUFNO TO A-NUMBER-OF-BUFFERS.

     MOVE SYSOCLAS TO A-SYSOUT-CLASS.
     MOVE SYSODEST TO A-SYSOUT-DEST.
     MOVE SYSODISP TO A-SYSOUT-DISP.

     IF SYSOCOPY NUMERIC
         MOVE SYSOCOPY TO A-SYSOUT-COPIES.

     CALL 'DDALLOC' USING DDALLOC-ALLOCATION-FIELDS.
     MOVE RETURN-CODE TO RECODE.
     MOVE A-ERROR-REASON TO RSCODE.

 300-EXIT.
     EXIT.
     EJECT

 600-UNALLOCATE.

     MOVE DSNAME TO UNAL-DSNAME.
     MOVE DDNAME TO UNAL-DDNAME.
     MOVE MEMNAME TO UNAL-MEMNAME.

     MOVE ZEROS TO RETURN-CODE.

     CALL 'DDUNAL' USING DDUNAL-UNALLOC-FIELDS.

     MOVE RETURN-CODE TO RECODE.
     MOVE UNAL-ERROR-REASON TO RSCODE.

 600-EXIT.
     EXIT.
     EJECT
 200-CALL-ISPF.
     CALL 'ISPLINK' USING I-FUNCTION
                          I-PARM1
                          I-PARM2
                          I-PARM3
                          I-PARM4
                          I-PARM5
                          I-PARM6.

 200-EXIT.
     EXIT.
     EJECT
 1000-INIT-RTN.
     MOVE SPACES TO DDALLOC-ALLOCATION-FIELDS
           ISPF-WS
           DDUNAL-UNALLOC-FIELDS.
     MOVE ZEROS TO RETURN-CODE.
     MOVE SPACES TO ISPF-PARAMETERS.
     MOVE 'CONTROL' TO I-FUNCTION.
     MOVE 'ERRORS' TO I-PARM1.
     MOVE 'RETURN' TO I-PARM2.
     PERFORM 200-CALL-ISPF
        THRU 200-EXIT.
     PERFORM 9900-VDEFINE-ISPF-VARIABLES
        THRU 9900-EXIT.

 1000-EXIT.
     EXIT.
     EJECT
*****************************************************************

 9900-VDEFINE-ISPF-VARIABLES.
*****************************************************************
     MOVE +44 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-44-LIST
                          LEN-44-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +8 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-8-LIST
                          LEN-8-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +6 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-6-LIST
                          LEN-6-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +5 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-5-LIST
                          LEN-5-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +4 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-4-LIST
                          LEN-4-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.

     MOVE +3 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-3-LIST
                          LEN-3-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +2 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-2-LIST
                          LEN-2-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.
     MOVE +1 TO VARIABLE-LENGTH-1.
     CALL 'ISPLINK' USING VDEFINE
                          LEN-1-LIST
                          LEN-1-WS
                          CHARACTER-FORMAT
                          VARIABLE-LENGTH-1
                          NOBSCAN-OPTION.

 9900-EXIT.
     EXIT.
     EJECT
