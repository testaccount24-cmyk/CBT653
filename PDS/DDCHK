******************************************************************
*  FUNCTION: INDICATE WHETHER A DDNAME IS ALLOCATED              *
*            AND THE NAME OF THE DATASET THAT IS ALLOCATED TO IT *
*            THIS IS SET UP TO RUN UNDER TSO THAT'S WHY THERE IS *
*            A HALFWORD "FILLER" IN THE PARAMETER DSECT.         *
*                                                                *
*  WRITTEN:         APR 1,  1986                                 *
*  AUTHOR:          JIM HARRISON                                 *
*  FOR MORE INFORMATION...: IBM OS/VS2 SPL:JOB MANAGEMENT        *
******************************************************************
*                      REGISTERS                                 *
******************************************************************
*        1=REQUEST BLOCK POINTER                                 *
*        4=BASE REGISTER                                         *
*        6=PARMS DSECT POINTER                                   *
*       12=LINKAGE                                               *
*       14=POINTER                                               *
*       15=RETURN CODE/POINTER                                   *
******************************************************************
         PRINT NOGEN
DDSDDCHK START 0
         REGISTER                register equates
         DS    0H
         STM   14,12,12(13)                      SAVE REGISTERS
         BALR  R4,0
         USING *,R4
         L     R6,R0(1)      -GET ADDRESS OF INPUT PARMS
         USING PARMS,R6     -USE PARMS DSECT TO REFERENCE
         DS    0H
         MVI   DYNSP1,X'00'            SET PARAMETER LIST TO LOW VALUES
         MVC   DYNSP1+1,DYNSP1
***********************************************************************
**   INITIALIZATION FOR ALLOCATION REQUEST                           **
***********************************************************************
         LA    R1,DYNSP1               LOAD ADDRESS OF PARAM LIST
         USING DYN1DS,R1               USE GENERATED DSECT
         LA    R15,DYN1RB              LOAD ADDRESS OF S99 RB
         USING S99RB,R15
         ST    R15,0(R1)               AND STORE IN RB POINTER
         XC    4(DYN1LEN-4,R1),4(R1)   ZERO PARAMETER LIST
         MVI   S99RBLN,20              MOVE IN LIST LENGTH(20)
         MVI   S99VERB,S99VRBIN  ----->MOVE IN VERB CODE(07)
         LA    R14,DYN1TUP             LOAD ADDRESS OF TU POINTERS
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB
         LA    R15,DYN1TU              POINT TO SPACE FOR TEXT UNITS
         USING S99TUNIT,R15
***********************************************************************
**   DDNAME TEXT UNIT   X'0001'                                      **
***********************************************************************
         MVI   S99TUKEY+1,DINDDNAM     SET TEXT UNIT KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         MVI   S99TULNG+1,8            MOVE IN LENGTH OF DDNAME
         MVC   S99TUPAR(8),DDNAME     MOVE IN DDNAME  NAME
         ST    R15,DYN1TUP             STORE TEXT UNIT ADDRESS
         LA    R15,14(R15)             BUMP TEXT UNIT PTR TO NEXT SLOT
***********************************************************************
**    DSNAME RETRIEVE UNIT X'0005'                                   **
***********************************************************************
         MVI   S99TUKEY+1,DINRTDSN     SET TEXT UNIT KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         MVI   S99TULNG+1,X'2C'        MOVE IN LENGTH OF DSN
         MVI   S99TUPAR,X'40'          CLEAR OUT DSNAME FIELD
         MVC   S99TUPAR+1(43),S99TUPAR
         LA    R10,S99TUPAR            SAVE ADDRESS OF DSNAME
         ST    R15,DYN1TUP+4           STORE TEXT UNIT ADDRESS
***********************************************************************
**   WRAP UP TEXT UNIT BUILDING ** TURN ON PERMANENT ATTRIBUTE       **
***********************************************************************
         MVI   DYN1TUP+4,X'80'        SET HIGH ORDER BIT ON TEXT PTRS
         MVI   DYN1RBP,X'80'           SET HIGH ORDER BIT ON RB PTR
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY
         LA    R14,4(R1)               POINT TO REQUEST BLOCK
         SVC   99
         USING DYN1RB,R14              SET UP ADDRESSABILITY
*
** R14 = RB ADDRESS
** R15 = RETURN CODE
*
DYN1DS   DSECT    -- DSECT TO MAP SVC 99 DATA -- 256 BYTE MAX ---
DYN1RBP   DS   F                       SVC 99 REQ BLOCK POINTER
DYN1RB    DS   5F                      SVC 99 REQUEST BLOCK
DYN1TUP   DS   CL70                    SPACE FOR TEXT POINTERS
DYN1TU    DS   CL162                   SPACE FOR TEXT UNITS(DYNSP1)
DYN1LEN   EQU  *-DYN1RBP               LENGTH OF SPACE USED
*
DDSDDCHK CSECT
         LR    8,14               GET ADDRESS OF REQUEST BLOCK
         USING S99RB,8            -ESTABLISH ADDRESSABILITY
         MVC   DSNAMEH(44),0(R10)        GET DATASET NAME
         MVC   DSNAME(44),DSNAMEH        MOVE IT TO LINKGE
         CLC   DSNAMEH,=C'NULLFILE'
         BE    RC4                   IF NULLFILE, SET RETURN CODE = 4
         CLI   DSNAMEH,C' '
         BNE   EXIT                  IF NOT SPACES, EXIT
         L     R15,=F'12'            ELSE SET RETURN-CODE = 12
         B     EXIT
RC4      EQU   *
         L     R15,=F'4'
EXIT     EQU   *
         RETURN (14,12),,RC=(15)
*
         DS    0F
DYNSP1   DS    CL256      SVC PARAMETER STORAGE MAPPED BY DSECT
*                          -THIS IS WHAT YOU WANT TO LOOK AT IN TEST -
SAVE     DC    18F'0'
DSNAMEH  DS    CL44
*
         PRINT GEN
* DSECT MACRO
         IEFZB4D0
*
* MNEMONICS MACRO
         IEFZB4D2
*
PARMS    DSECT
PARMLEN  DS    H
DDNAME   DS    CL8
DSNAME   DS    CL44        RETURNED TO PROGRAM
         CSECT          PANVALET TIME STAMP
PANVALET DC    CL47'001DDSDDCHK  03/25/98 COMPILED 98085 -  9:58:00'
         END   DDSDDCHK
