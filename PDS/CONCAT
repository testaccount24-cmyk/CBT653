*          DATA SET D3CNCTXP   AT LEVEL 001 AS OF 03/12/86
******************************************************************
**  SDCNCT:                                                     **
**  THIS IS A SUBROUTINE TO DYNAMICALLY CONCATENATE OR          **
**  DECONCATENATE DATASETS USING DDNAMES. (MAXIMUM OF 5)        **
******************************************************************
** WRITTEN:         NOV 29, 1984                                **
** AUTHOR:          JIM HARRISON/DRMB                           **
******************************************************************
**                     REGISTERS                                **
******************************************************************
**       1=REQUEST BLOCK POINTER FOR SVC 99                     **
**       4=BASE REGISTER                                        **
**       6=PARAMETER ADDRESSING                                 **
**       9=DDNAME TABLE POINTER                                 **
**      10=BCT COUNTER                                          **
**      12=LINKAGE                                              **
**      14=POINTER                                              **
**      15=RETURN CODE/POINTER                                  **
******************************************************************
         PRINT NOGEN
SDCNCT   START 0
         REGS
         DS    0H
         STM   14,12,12(13)                      SAVE REGISTERS
         BALR  4,0
         USING *,4
         L     R6,R0(1)    -GET ADDRESS OF INPUT PARMS
         USING PARMS,R6     -USE PARM DSECT TO REFERENCE
*
         DS    0H
         MVI   DYNSP1,X'00'            SET PARAMETER LIST TO LOW VALUES
         MVC   DYNSP1+1,DYNSP1
***********************************************************************
**   INITIALIZATION FOR ALLOCATION ROUTINE                           **
***********************************************************************
         LA    R1,DYNSP1               LOAD ADDRESS OF PARAM LIST
         USING DYN1DS,R1               USE GENERATED DSECT
         LA    R15,DYN1RB              LOAD ADDRESS OF S99 RB
         USING S99RB,R15
         ST    R15,0(R1)               AND STORE IN RB POINTER
         XC    4(DYN1LEN-4,R1),4(R1)   ZERO PARAMETER LIST
         MVI   S99RBLN,20              MOVE IN LIST LENGTH(20)
***********************************************************************
**   CHECK FOR REQUEST TYPE  C=CONCATENATION                         **
***********************************************************************
         CLI   CODE,C'C'               CHECK FOR TYPE OF REQUEST
         BE    CONCAT                  IF 'C'    GOTO CONCAT
         MVI   S99VERB,S99VRBDC        ELSE MOVE IN DECONCAT CODE (04)
         B     ENDCON
CONCAT   MVI   S99VERB,S99VRBCC        MOVE IN CONCAT CODE (03)
ENDCON   LA    R14,DYN1TUP             LOAD ADDRESS OF TU POINTERS
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB
         LA    R15,DYN1TU              POINT TO SPACE FOR TEXT UNITS
         USING S99TUNIT,R15
***********************************************************************
**   DDNAME TEXT UNIT   X'0001'                                      **
***********************************************************************
         ST    R15,DYN1TUP+0           STORE TEXT UNIT ADDRESS
         LA    R9,DDNAMES              SET R9 TO TOP OF DDNAME-TABLE
         MVC   DOUB,LOWVAL
         PACK  DOUB+7(1),NUMBER(1)
         CVB   R10,DOUB                SET R10 FOR BCT LOOP
         STC   R10,NUMBER              PUT BINARY VALUE OF NUMBER
*                                          INTO NUMBER FIELD.
         MVI   S99TUKEY+1,DCCDDNAM     SET TEXT UNIT KEY
         MVC   S99TUNUM+1(1),NUMBER       SET NUMBER OF DDNAMES
LOADNAME EQU   *
         MVI   S99TULNG+1,8            MOVE IN LENGTH OF DDNAME
         MVC   S99TUPAR(8),0(R9)       MOVE IN DDNAME FROM TABLE
BUMP     LA    R15,10(R15)             BUMP TEXT UNIT PTR TO NEXT SLOT
         MVI   BUMP+3,X'0A'            SET DISPL TO 10 AFTER 1ST MVI
         LA    R9,8(R9)                BUMP UP DDNAME-TABLE POINTER
         BCT   R10,LOADNAME            LOOP BACK
*
***********************************************************************
**   WRAP UP TEXT UNIT BUILDING *                                    **
***********************************************************************
         MVI   DYN1TUP+0,X'80'        SET HIGH ORDER BIT ON TEXT PTRS
         MVI   DYN1RBP,X'80'           SET HIGH ORDER BIT ON RB PTR
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY
         LA    R14,4(R1)               POINT TO REQUEST BLOCK
         SVC   99           *** DO IT ****
         USING DYN1RB,R14              SET UP ADDRESSABILITY
** R14 = RB ADDRESS
** R15 = RETURN CODE
*
DYN1DS   DSECT  **MAX SIZE 256**       DSECT TO MAP SVC 99 DATA
DYN1RBP   DS   F                       SVC 99 REQ BLOCK POINTER
DYN1RB    DS   5F                      SVC 99 REQUEST BLOCK
DYN1TUP   DS   CL70                    SPACE FOR TEXT POINTERS
DYN1TU    DS   CL162                   SPACE FOR TEXT UNITS(DYNSP1)
DYN1LEN   EQU  *-DYN1RBP               LENGTH OF SPACE USED
*
SDCNCT CSECT
         LR    8,14               GET ADDRESS OF REQUEST BLOCK
         USING S99RB,8            -ESTABLISH ADDRESSABILITY
EXIT     EQU   *
         RETURN (14,12),,RC=(15)
*
         DS    0F
DYNSP1   DS    CL256      SVC PARAMETERS FOR ALLOCATION REQUEST
*                          -THIS IS WHAT YOU WANT TO LOOK AT IN TEST -
SAVE     DC    18F'0'
LOWVAL   DC    D'0'
DOUB     DC    D'0'
* DSECT MACRO
         IEFZB4D0
*
* MNEMONICS MACRO
         IEFZB4D2
*
**********************************************************************
*   ALLOCATION PARAMETERS                                            *
**********************************************************************
PARMS    DSECT
PARMINFO DS    0CL42
CODE     DS    CL1         ALLOCATION CODE (C=CONCAT D=DECONAT)
NUMBER   DS    CL1         NUMBER OF DDNAMES IN  PARM
DDNAMES  DS    CL8         DDNAME 1
         DS    CL8         DDNAME 2
         DS    CL8         DDNAME 3
         DS    CL8         DDNAME 4
         DS    CL8         DDNAME 5
         CSECT          PANVALET TIME STAMP
PANVALET DC    CL47'001D3CNCTXP  03/12/86 COMPILED 86212 - 10:19:30'
         END   SDCNCT
